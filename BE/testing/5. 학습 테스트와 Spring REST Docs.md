# 학습 테스트와 Spring REST Docs

<br>

## 학습 테스트

- 잘 모르는 기능, 라이브러리, 프레임워크를 학습하기 위해 작성하는 테스트
- 여러 테스트 케이스를 스스로 정의하고 검증하는 과정을 통해 보다 구체적인 동작과 기능을 학습할 수 있다.
- 관련 문서만 읽는 것보다 재미있게 학습할 수 있다.

<br>

### guava 라이브러리로 학습 테스트 작성해보기

[GuavaLearningTest.java](https://github.com/jmxx219/TIL/blob/main/Testing/cafekiosk/src/test/java/sample/cafekiosk/learning/GuavaLearningTest.java)

- Guava는 구글이 작성한 자바 오픈소스 라이브러리로, 유용한 유틸리티 함수와 클래스를 다양하게 제공한다.

<br>
<br>


## Spring REST Docs

- 테스트 코드를 통한 API 문서 자동화 도구
- API 명세를 문서로 만들고 외부에 제공함으로써 협업을 원할하게 한다.
- 기본적으로 AsciiDoc을 사용하여 문서를 작성한다.

<br>

### REST Docs 작성 방법

#### build.gradle

```Java
plugins {
    ...
	id "org.asciidoctor.jvm.convert" version "3.3.2" // (1)
}

configurations {
    ...
	asciidoctorExt // (2)
}

dependencies {
	// (3) 
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

ext { // 전역 변수
	snippetsDir = file('build/generated-snippets') // (4)
}

test {
	outputs.dir snippetsDir // (5)
}

asciidoctor {
	inputs.dir snippetsDir // (6)
	configurations 'asciidoctorExt' // (7)

	sources { // (8)
		include("**/index.adoc")
	}
	baseDirFollowsSourceFile() // (9)
	dependsOn test // (10)
}

bootJar { // (11)
	dependsOn asciidoctor 
	from("${asciidoctor.outputDir}") {
		into 'static/docs'
	}
}
```
- (1) asciidoctor 플러그인 추가
- adoc 파일을 변환하고 build 디렉토리에 복사하기 위해 사용하는 플러그인
- gradle 7부터는 `org.asciidoctor.convert` 대신 `asciidoctor.jvm.convert`를 사용함
- (2) asciidoctor를 확장하는 종속성에 대한 구성을 선언
- (3) REST Docs 의존성 추가
- (4) snippets 파일이 저장될 경로를 설정
  - 여러 라이브러리 모듈이 포함되어 있을 때 모듈 간 공유할 수 있는 변수를 `ext`에 선언함
- (5) 출력할 디렉토리 설정
  - test 작업 시, output을 위에서 설정한 snippetsDir에 넣도록 함
- (6) input 디렉토리를 설정
- (7) asciidoctor에서 asciidoctorExt을 configurations로 사용하도록 설정
- (8) 특정 파일만 html로 만듦
- (9) adoc 파일에서 다른 adoc 파일을 include 할 때 경로를 baseDir로 동일하게 설정함
- (10) build 시, test 작업 후에 asciidoctor를 진행하도록 설정함(순서 설정)
- (11) 프로젝트를 Jar 파일로 생성할 때, Rest Docs가 해당 경로에 들어갈 수 있도록 함

<br>

#### 실행 순서

1. api controller 테스트 코드 작성(rest docs 코드 추가)
2. `gradle`에서 documentation/asciidoctor 실행
3. 여기까지하면 로컬에서 돌렸을 때 api 문서 확인 가능
   - `src/docs/asciidoc/index.adoc` 크롬에서 열어서 확인 가능
4. build 시, html로 api 문서 보기 
   4-1. jar 파일 실행 후, `http://localhost:8080/docs/index.html` 열기


<br>

### REST Docs vs Swagger
- Spring REST Docs
  - 장점
      - 테스트를 통과해야 문서가 만들어지기 때문에 신뢰도가 높다.
      - 프로덕션 코드에 비침투적이다.
  - 단점
      - 코드 양이 많다.
      - 설정이 어렵다.
- Swagger
  - 장점
    - 적용이 쉽다.
    - 문서에서 바로 API 호출을 수행해볼 수 있다.
  - 단점
      - 프로덕션 코드에 침투적이다.
      - 테스트와 무관하기 때문에 신뢰도가 떨어질 수 있다.